defmodule Electric.Postgres.Extension.Migrations.Migration_20230424154425_DDLX do
  alias Electric.Postgres.Extension

  @behaviour Extension.Migration
  sql_file = Path.expand("20230424154425_ddlx/ddlx-14.sql", __DIR__)
  @external_resource sql_file
  @split_exp "CREATE OR REPLACE FUNCTION"
  @sql File.read!(sql_file)
       |> String.split(@split_exp)
       |> tl()
       |> Enum.map(&(@split_exp <> &1))

  # the `ddls-14.sql` file is generated by the scripts in
  # https://github.com/electric-sql/pgddl see the `README-electric.md` file for
  # reasons and instructions.

  @impl true
  def version, do: 2023_04_24_15_44_25

  @impl true
  def up(schema) do
    Enum.map(@sql, &String.replace(&1, "@schemaname@", schema)) ++ overrides(schema)
  end

  # rather than add too many modifications to the upstream functions, just overwrite
  # some functions that output stuff we don't want.
  defp overrides(schema) do
    [
      # ddlx_comment adds `COMMENT ON` statements that we don't want or support
      """
      CREATE OR REPLACE FUNCTION #{schema}.ddlx_comment(oid) RETURNS text LANGUAGE sql AS $function$
          select ''::text;
      $function$ strict;
      """,
      # this just outputs a comment header, but it's just noise we don't want
      """
      CREATE OR REPLACE FUNCTION #{schema}.ddlx_banner(name text, kind text, namespace text, owner text, extra text default null)
      RETURNS text LANGUAGE sql AS $function$
         select ''::text;
      $function$;
      """,
      # this function appends a list of constraints as alter table commands
      # after table definitions. our patch moves those into the create table
      # block so just remove
      """
      CREATE OR REPLACE FUNCTION #{schema}.ddlx_create_constraints(regclass)
        RETURNS text LANGUAGE sql AS $function$
          SELECT NULL;
      $function$  strict;
      """,
      # this function adds a bunch of alter table add default commands
      # after table definitions. our patch moves those into the create table
      # block so just remove. sqlite doesn't support ALTER TABLE SET DEFAULT
      # so we need to fix.
      """
      CREATE OR REPLACE FUNCTION #{schema}.ddlx_alter_table_defaults(regclass)
        RETURNS text LANGUAGE sql AS $function$
          SELECT NULL;
      $function$ strict;

      """
    ]
  end

  @impl true
  def down(_schema) do
    []
  end
end
