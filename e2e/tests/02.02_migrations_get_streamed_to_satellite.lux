[doc Migrations get correctly streamed to Satellite]
[global fail_pattern=[Ee][Rr][Rr][Oo][Rr]]
[include _shared.luxinc]

[invoke setup]
# Start without electrified tables

[global user_id_1=1]
[newshell user_1_ws1]
    -$fail_pattern
    [invoke start_elixir_test 1]
    [invoke client_session $user_id_1 1]

[global migration_version=20230504114018]
[shell pg_1]
    """!
    BEGIN;
    SELECT electric.migration_version('$migration_version');
    CREATE TABLE mtable1 (id uuid PRIMARY KEY);
    CALL electric.electrify('mtable1');
    COMMIT;
    """
    ?$psql

[shell electric]
    # We expect to send the migration to Satellite
    ?client_id=client_1_1 .* \[info\] Serializing migration "$migration_version"

[shell user_1_ws1]
    # We don't want to see shadow table in a migration streamed to Satellite
    -shadow__public__entries|$fail_pattern
    # And recieve it there
    ?.*SatOpMigrate\{.*sql: "CREATE TABLE \\"mtable1\\".*

[cleanup]
    [invoke teardown]
