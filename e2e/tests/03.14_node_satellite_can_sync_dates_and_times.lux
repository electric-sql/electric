[doc NodeJS Satellite correctly syncs DATE and TIME values from and to Electric]
[include _shared.luxinc]
[include _satellite_macros.luxinc]

[invoke setup]

[shell pg_1]
    [local sql=
        """
        CREATE TABLE public.datetimes (
            id TEXT PRIMARY KEY DEFAULT uuid_generate_v4(),
            d DATE,
            t TIME
        );
        CALL electric.electrify('public.datetimes');
        """]
    [invoke migrate_pg 20230913 $sql]

[invoke setup_client 1 electric_1 5133]

[shell satellite_1]
    [invoke node_await_table "name: 'datetimes'"]
    [invoke node_sync_table "datetimes"]

[shell pg_1]
    !INSERT INTO public.datetimes (id, d, t) VALUES ('001', '2023-08-23', '11:00:59'), \
                                                    ('002', '01-01-0001', '00:59:03.11'), \
                                                    ('003', 'Feb 29 6000', '23:05:17.999999');
    ??INSERT 0 3

[shell satellite_1]
    [invoke node_await_assert_datetime "001" "2023-08-23" "1970-01-01 11:00:59"]
    [invoke node_await_assert_datetime "002" "0001-01-01" "1970-01-01 00:59:03.110"]
    [invoke node_await_assert_datetime "003" "Feb 29 6000" "1970-01-01 23:05:17.999999"]

    !await client.write_datetime(db, { id: '004', d: new Date('1999-12-31'), t: new Date('1996-02-23 14:46:05.42') })
    ?$node
    !await client.write_datetime(db, { id: '005', d: new Date('2023-09-26'), t: new Date('2022-11-12 00:00:00.000') })
    ?$node

[shell pg_1]
    [invoke wait-for "SELECT * FROM public.datetimes;" "005" 10 $psql]

    !SELECT * FROM public.datetimes;
    ??001 | 2023-08-23 | 11:00:59
    ??002 | 0001-01-01 | 00:59:03.11
    ??003 | 6000-02-29 | 23:05:17.999999
    ??004 | 1999-12-31 | 14:46:05.42
    ??005 | 2023-09-26 | 00:00:00

# Start a new Satellite client and verify that it receives all dates and times
[invoke setup_client 2 electric_1 5133]

[shell satellite_2]
    [invoke node_await_table "datetimes"]
    [invoke node_sync_table "datetimes"]

    [invoke node_await_assert_datetime "001" "2023-08-23" "1970-01-01 11:00:59"]
    [invoke node_await_assert_datetime "002" "0001-01-01" "1970-01-01 00:59:03.11"]
    [invoke node_await_assert_datetime "003" "Feb 29 6000" "1970-01-01 23:05:17.999999"]
    [invoke node_await_assert_datetime "004" "1999-12-31" "1970-01-01 14:46:05.42"]
    [invoke node_await_assert_datetime "005" "2023-09-26" "1970-01-01 00:00:00.000"]

[cleanup]
  [invoke teardown]
