[doc When NodeJS Satellite connects to Electric for the first time, it receives all migrations in one go]
[include _shared.luxinc]
[include _satellite_macros.luxinc]

[invoke setup]

# Insert a few rows to 'entries' before electrifying it.
[shell pg_1]
  !INSERT INTO entries (id, content) VALUES ('00000000-0000-0000-0000-000000000001', 'first entry');
  ?INSERT 0 1
  !INSERT INTO entries (id, content) VALUES ('00000000-0000-0000-0000-000000000002', 'second entry');
  ?INSERT 0 1

[invoke electrify_table pg_1 entries]

[global migration_vsn=20230704150000]

# Create a new table, electrify it immediately, and then insert a few rows.
[shell pg_1]
  [local sql=
    """
    CREATE TABLE public.items (
      id UUID PRIMARY KEY DEFAULT uuid_generate_v4(),
      content VARCHAR(64) NOT NULL
    );
    CALL electric.electrify('public.items');
    """]

  [invoke migrate_pg $migration_vsn $sql]

  !INSERT INTO items(id, content) VALUES ('00000000-0000-0000-0000-000000000011', 'first item');
  ?INSERT 0 1
  !INSERT INTO items(id, content) VALUES ('00000000-0000-0000-0000-000000000012', 'second item');
  ?INSERT 0 1

# Start a Satellite client and verify that it gets all migrations during initial sync.
[invoke setup_client 1 "electric_1" 5133]

[shell satellite_1]
  # Verifying it's a fresh client
  ?applying migration: 0
  ?no lsn retrieved from store
  ?connecting and starting replication
  ?no previous LSN, start replication with option FIRST_LSN

  ?Sending message \{"\$$type":"Electric.Satellite.v[0-9_]+.SatInStartReplicationReq","lsn":\{\}
  ?Received message \{"\$$type":"Electric.Satellite.v[0-9_]+.SatInStartReplicationResp"\}

  # Verifying the initial sync

  ## The client first receives migrations corresponding to all electrified tables on the server.
  ##
  ## We know the order in which the migrations are streamed because
  ##
  ##   a) the version of the migration that created the "items" table is static.
  ##   b) the version of the migration that created the "entries" table is the e2e test suite start time.
  ?Received message \{"\$$type":"Electric.Satellite.v[0-9_]+.SatRelation","schemaName":"public",.*"tableName":"items"
  ?Received message \{"\$$type":"Electric.Satellite.v[0-9_]+.SatOpLog","ops":\[\
    \{"\$$type":"Electric.Satellite.v[0-9_]+.SatTransOp","begin":\{.*\}\},\
    \{"\$$type":"Electric.Satellite.v[0-9_]+.SatTransOp","migrate":\{.*,"version":"$migration_vsn",.*,"table":\{.*,"name":"items"

  ?Received message \{"\$$type":"Electric.Satellite.v[0-9_]+.SatRelation","schemaName":"public",.*"tableName":"entries"
  ?Received message \{"\$$type":"Electric.Satellite.v[0-9_]+.SatOpLog","ops":\[\
    \{"\$$type":"Electric.Satellite.v[0-9_]+.SatTransOp","begin":\{.*\}\},\
    \{"\$$type":"Electric.Satellite.v[0-9_]+.SatTransOp","migrate":\{.*,"version":"[0-9_]+",.*,"table":\{.*,"name":"entries"


[cleanup]
  [invoke teardown]
