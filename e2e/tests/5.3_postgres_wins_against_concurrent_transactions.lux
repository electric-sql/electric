[doc In case a transaction comes while the transaction has been opened, PG picks a higher tag to win]
[include _shared.luxinc]

[invoke setup]
[invoke electrify_table pg_1 entries]

[newshell user_1_ws1]
    -$fail_pattern
    [invoke start_elixir_test 1]
    [invoke client_session 1 1]
    !Electric.Test.SatelliteWsClient.send_test_relation(conn)
    ?:ok
    ?$eprompt

[shell pg_1]
    !\pset tuples_only
    # Given an already-inserted row
    !INSERT INTO entries (id, content) VALUES ('00000000-0000-0000-0000-000000000000', 'original value');
    ?$psql
    # We start a transaction
    !BEGIN;
    ?$psql

[shell user_1_ws1]
    ?%Electric.Satellite.V\d+.SatOpInsert\{.*tags: \["(postgres_1@\d+)"\]
    [my seen_tag=$1]
    # We do an update while transaction on PG is open
    """!
    Electric.Test.SatelliteWsClient.entries_table_send_update(
        conn,
        "1", # lsn
        DateTime.utc_now() |> DateTime.add(86400, :second) |> DateTime.to_unix(:millisecond), # + 1 day
        "00000000-0000-0000-0000-000000000000", # id
        %{"content" => "original value", "content_b" => nil}, # old
        %{"content" => "updated on client 1", "content_b" => nil}, # new
        ["$seen_tag"]
    )
    """
    ?:ok
    ?$eprompt

[shell electric]
    # Wait for it to be sent to PG
    ??pg_slot=postgres_1 [debug] Will send 1 to subscriber:
    ?pg_slot=postgres_1 \[debug\] Sending 6 messages to the subscriber: from (#Lsn<0/29>)
    [my expected_lsn=$1]
    ??<StatusUpdate> [written_wal: $expected_lsn

[shell pg_1]
    # And then we also do an update here
    """!
    UPDATE entries SET content = 'updated on pg' WHERE id = '00000000-0000-0000-0000-000000000000';
    COMMIT;
    """
    ?$psql
    # Expecting the update to win
    !SELECT content FROM entries WHERE id = '00000000-0000-0000-0000-000000000000';
    ??updated on pg
    ?$psql
    # By setting the column tag to the same timestamp, but source to `null`
    !SELECT (shadow.content).source_id, (shadow.content).timestamp > (CURRENT_TIMESTAMP + interval '23 hours') FROM electric.shadow__public__entries as shadow WHERE id = '00000000-0000-0000-0000-000000000000';
    ?<NULL>\s+\| t

[shell user_1_ws1]
    # And then expect to see the same row sent from Electric back to Satellite, keeping commit timestamp
    ?rec \[\d\]: %Electric.Satellite.V\d+.SatOpLog\{(.*)origin: "postgres_1"(.*)values: \["00000000-0000-0000-0000-000000000000", "updated on pg", ""\]

[cleanup]
    [invoke teardown]