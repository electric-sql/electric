[doc NodeJS Satellite receives missing data for previously synced tables when reconnecting]
[include _shared.luxinc]
[include _satellite_macros.luxinc]

[invoke setup]

[invoke setup_client 1 "electric_1" 5133]

[shell pg_1]
    [local sql=
        """
        CREATE TABLE public.items (
        id UUID PRIMARY KEY DEFAULT uuid_generate_v4(),
        content VARCHAR(64) NOT NULL,
        content_text_null VARCHAR(64),
        content_text_null_default VARCHAR(64) DEFAULT '',
        intvalue_null integer,
        intvalue_null_default integer DEFAULT 10
        );
        CALL electric.electrify('public.items');
        """]
    [invoke migrate_pg 20230504114018 $sql]

[shell satellite_1]
    ?Received message \{"\$$type":"Electric.Satellite.v\d+_\d+.SatInStartReplicationResp"\}
    [invoke node_await_table "items"]
    # We shouldn't see this "hello from pg" until we actually do sync
    -(hello from pg|$fail_pattern)

[shell pg_1]
    # Insert a row
    !INSERT INTO public.items (id, content) VALUES ('00000000-0000-0000-0000-000000000000', 'hello from pg');
    ?$psql

[shell satellite_1]
    -$fail_pattern
    [invoke node_sync_table "items"]
    # As soon as sync is done, we expect to see the row in the database
    !await client.get_items(db)
    ??hello from pg

[shell pg_1]
    # Insert another row
    !INSERT INTO public.items (id, content) VALUES ('00000000-0000-0000-0000-000000000001', 'hello from pg again');
    ?$psql

[shell satellite_1]
    # This one gets propagated "normally"
    [invoke node_await_get "00000000-0000-0000-0000-000000000001"]
    !await client.delete_item(db, ['hello from pg again'])
    ?$node

[shell pg_1]
    !\x
    [invoke wait-for "SELECT COUNT(*) FROM public.items;" "count \| 1" 10 ${psql}]

[shell satellite_1]
    [progress stopping client]
    !await client.stop(db)
    ?$node
    [progress resuming client]
    [invoke electrify_db "originalDb" "electric_1" 5133 "[]"]
    ?$node

[shell pg_1]
    [progress pg_1 is writing a new row]
    # Insert another row
    !INSERT INTO public.items (id, content) VALUES ('00000000-0000-0000-0000-000000000002', 'hello from pg after resume');
    ?$psql

[shell satellite_1]
    [progress satellite_1 waiting for new row to arrive without having to resubscribe]
    [invoke node_await_get "00000000-0000-0000-0000-000000000002"]

[cleanup]
  [invoke teardown]
