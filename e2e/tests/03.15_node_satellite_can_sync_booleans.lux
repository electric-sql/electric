[doc NodeJS Satellite correctly syncs BOOL values from and to Electric]
[include _shared.luxinc]
[include _satellite_macros.luxinc]

[invoke setup]

[shell proxy_1]
    [local sql=
        """
        CREATE TABLE public.bools (
            id TEXT PRIMARY KEY,
            b BOOLEAN
        );
        ALTER TABLE public.bools ENABLE ELECTRIC;
        ELECTRIC GRANT ALL ON public.bools TO AUTHENTICATED;
        """]
    [invoke migrate_pg 20230908 $sql]

[invoke setup_client 1 electric_1 5133]

[shell satellite_1]
    [invoke node_await_table "bools"]
    [invoke node_sync_table "bools"]

[shell pg_1]
    !INSERT INTO public.bools (id, b) VALUES ('001', true), ('002', false), ('003', NULL);
    ??INSERT 0 3

[shell satellite_1]
    [invoke node_await_get_bool "001" "true"]
    [invoke node_await_get_bool "002" "false"]
    [invoke node_await_get_bool "003" "null"]

    !await client.write_bool(db, '004', true)
    ?{ id: '004', b: true }
    ?$node

[shell pg_1]
    [invoke wait-for "SELECT * FROM public.bools;" "004" 10 $psql]

    !SELECT * FROM public.bools;
    ??001 | t
    ??002 | f
    ??003 | <NULL>
    ??004 | t

# Start a new Satellite client and verify that it receives all rows
[invoke setup_client 2 electric_1 5133]

[shell satellite_2]
    [invoke node_await_table "bools"]
    [invoke node_sync_table "bools"]

    [invoke node_await_get_bool "001" "true"]
    [invoke node_await_get_bool "002" "false"]
    [invoke node_await_get_bool "003" "null"]
    [invoke node_await_get_bool "004" "true"]

[cleanup]
  [invoke teardown]
