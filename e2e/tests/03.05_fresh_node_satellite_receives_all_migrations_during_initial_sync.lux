[doc When NodeJS Satellite connects to Electric for the first time, it receives all migrations in one go]
[include _shared.luxinc]
[include _satellite_macros.luxinc]

[invoke setup]

# Insert a few rows to 'entries' before electrifying it.
[shell pg_1]
  !INSERT INTO entries (id, content) VALUES ('00000000-0000-0000-0000-000000000001', 'first entry');
  ?INSERT 0 1
  !INSERT INTO entries (id, content) VALUES ('00000000-0000-0000-0000-000000000002', 'second entry');
  ?INSERT 0 1

[invoke electrify_table pg_1 entries]

# Create a new table, electrify it immediately, and then insert a few rows.
[shell pg_1]
  [local sql=
    """
    BEGIN;

    CREATE TABLE items (
      id UUID PRIMARY KEY DEFAULT uuid_generate_v4(),
      content VARCHAR NOT NULL
    );
    CALL electric.electrify('public.items');

    INSERT INTO items(id, content) VALUES ('00000000-0000-0000-0000-000000000011', 'first item');
    INSERT INTO items(id, content) VALUES ('00000000-0000-0000-0000-000000000012', 'second item');

    COMMIT;
    """]

  !$sql
  ?COMMIT

# Start a Satellite client and verify that it gets all migrations during initial sync.
[invoke setup_client 1 "electric_1" 5133]

[shell satellite_1]
  ??[rpc] send: #SatInStartReplicationReq
  ??[rpc] recv: #SatInStartReplicationResp

  # Verifying the initial sync. The client receives migrations corresponding to all electrified tables on the server.
  ??[proto] recv: #SatRelation{for: public.entries
  ?\[proto\] recv: #SatOpLog\{.*#Migrate\{vsn: [0-9_]+, for: entries

  ??[proto] recv: #SatRelation{for: public.items
  ?\[proto\] recv: #SatOpLog\{.*#Migrate\{vsn: [0-9_]+, for: items

[cleanup]
  [invoke teardown]
