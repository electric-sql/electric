[doc NodeJS Satellite can authenticate, renew its token and is informed when its token expires]
[include _shared.luxinc]
[include _satellite_macros.luxinc]

[invoke setup]

[invoke start_satellite 1]

[shell satellite_1]
    ??$node
    # Set a failure pattern so that the test will fail if JWT expires
    # before we want it to
    -JWT expired too early
    # Set expiration time for the JWT to 2 seconds from now
    !exp="2s"
    [invoke connect_to_electric electric_1 5133 "[]"]
    ?Connectivity state changed: connected
    # Check that we can renew the token
    !await client.renew_token(db, "5s")
    ??$node
    # Token should expire after > 4 seconds
    !client.check_token_expiration(db, 4000)
    ??$node
    ??JWT expired after
    # Check that we can reconnect
    !await client.reconnect(db, "3s")
    ??$node
    # Check that client is connected again
    ?Connectivity state changed: connected
    # Token should expire after > 2 seconds
    !client.check_token_expiration(db, 2000)
    ??$node
    ??JWT expired after

[cleanup]
    [invoke teardown]
