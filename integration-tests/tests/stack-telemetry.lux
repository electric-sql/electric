[doc Verify that all of the expected stack metrics are exported via Otel]

[include _macros.luxinc]

[global pg_container_name=stack-telemetry__pg]

###

[invoke setup_otel_collector]

[invoke setup_pg]

[invoke setup_electric_with_env "ELECTRIC_OTLP_ENDPOINT=http://localhost:4318 ELECTRIC_SYSTEM_METRICS_POLL_INTERVAL=1s ELECTRIC_OTEL_EXPORT_PERIOD=2s DO_NOT_START_CONN_MAN_PING=1 ELECTRIC_LOG_LEVEL=info OTEL_RESOURCE_ATTRIBUTES=custom.attr=electric.val"]

[shell otel_collector]
  # Verify that the Collector receives metrics from Electric with expected resource attributes
  ??info	ResourceMetrics
  ??Resource attributes:
  ??     -> custom.attr: Str(electric.val)
  ??     -> instance.id: Str(
  ??     -> name: Str(metrics)
  ??     -> service.name: Str(electric)
  ??     -> service.version: Str(

  # Verify that LSN metrics are exported
  """?
  Metric #[0-9]+
  Descriptor:
       -> Name: electric\.postgres\.replication\.pg_wal_offset
       -> Description: 
       -> Unit: 
       -> DataType: Gauge
  NumberDataPoints #0
  StartTimestamp: [-0-9]+ [.:0-9]+ [-+0-9]+ UTC
  Timestamp: [-0-9]+ [.:0-9]+ [-+0-9]+ UTC
  Value: [-+.0-9]+
  """

  """?
  Metric #[0-9]+
  Descriptor:
       -> Name: electric\.postgres\.replication\.slot_confirmed_flush_lsn_lag
       -> Description: 
       -> Unit: By
       -> DataType: Gauge
  NumberDataPoints #0
  StartTimestamp: [-0-9]+ [.:0-9]+ [-+0-9]+ UTC
  Timestamp: [-0-9]+ [.:0-9]+ [-+0-9]+ UTC
  Value: [-+.0-9]+
  """

  """?
  Metric #[0-9]+
  Descriptor:
       -> Name: electric\.postgres\.replication\.slot_retained_wal_size
       -> Description: 
       -> Unit: By
       -> DataType: Gauge
  NumberDataPoints #0
  StartTimestamp: [-0-9]+ [.:0-9]+ [-+0-9]+ UTC
  Timestamp: [-0-9]+ [.:0-9]+ [-+0-9]+ UTC
  Value: [-+.0-9]+
  """
[invoke start_psql]

[shell psql]
  !create table items (val text);
  ??CREATE

[shell client]
  [invoke curl_shape "http://localhost:3000/v1/shape?table=items&offset=-1"]

  ??HTTP/1.1 200 OK
  ?electric-handle: ([\d-]+)
  [global handle=$1]
  ?electric-offset: ([\w\d_]+)
  [global offset=$1]

[shell psql]
  !insert into items values ('3');
  ??INSERT

[shell client]
  [invoke curl_shape "http://localhost:3000/v1/shape?table=items&handle=$handle&offset=$offset&live"]

  ??HTTP/1.1 200 OK
  ??"value":{"val":"3"}

[shell otel_collector]
  """?
  Metric #[0-9]+
  Descriptor:
       -> Name: electric\.storage\.transaction_stored\.bytes
  """
###

[cleanup]
  [invoke teardown]
