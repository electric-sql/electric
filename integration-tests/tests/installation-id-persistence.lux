[doc Verify that an installation ID is generate on first launch and is then persisted across service restarts]

[include _macros.luxinc]

[global pg_container_name=installation-id-persistence__pg]

###

# Start a new Postgres cluster
[invoke setup_pg]

# Start the sync service.
[invoke setup_electric]

[shell electric]
  ??[info] Starting replication from postgres

  !instance_id = Electric.Config.fetch_env!(:instance_id)
  ?"([0-9a-f]{8}-([0-9a-f]{4}-){3}[0-9a-f]{12})"
  [my instance_id=$1]

  !{m, f, a} = Electric.Config.get_env(:persistent_kv)
  !kv = apply(m, f, [a])
  !installation_id = Electric.Config.installation_id!(kv)
  ?"([0-9a-f]{8}-([0-9a-f]{4}-){3}[0-9a-f]{12})"
  [my installation_id=$1]

  !instance_id == installation_id
  ??true

# Terminate electric
[shell electric]
  !System.halt()
  ??$PS1

# Start the sync service again.
[invoke setup_electric]

[shell electric]
  ??[info] Starting replication from postgres

  # Verify the same installation ID is retained, even though the instance ID has changed
  !{m, f, a} = Electric.Config.get_env(:persistent_kv)
  !kv = apply(m, f, [a])
  !installation_id = Electric.Config.installation_id!(kv)

  !">> installation_id = #{installation_id}"
  ??>> installation_id = $installation_id

  !instance_id = Electric.Config.fetch_env!(:instance_id)
  !instance_id == installation_id
  ??false

  !instance_id == "$instance_id"
  ??false

[cleanup]
  [invoke teardown]
