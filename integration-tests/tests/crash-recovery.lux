[doc Verify handling of an Electric crash recovery]

[include _macros.luxinc]

[global pg_container_name=crash-recovery__pg]

###

## Start a new Postgres cluster
[invoke setup_pg]

## Add some data
[invoke start_psql]
[shell psql]
  """!
  CREATE TABLE items (
    id UUID PRIMARY KEY,
    val TEXT
  );
  """
  ??CREATE TABLE

  """!
  INSERT INTO
    items (id, val)
  SELECT
    gen_random_uuid(),
    '#' || generate_series || ' test val'
  FROM
    generate_series(1, 10);
  """
  ??INSERT 0 10

## Start the sync service.
[invoke setup_electric]

[shell electric]
  ??[info] Starting replication from postgres

# Initialize a shape and collect the offset
[shell client]
  [invoke shape_get_snapshot items]
  ?electric-handle: ([\d-]+)
  [local handle=$1]
  ?electric-offset: ([\w\d_]+)
  [local offset=$1]

## Crash electric
[shell electric]
  !System.halt()
  ??$PS1

## Start the sync service again.
[invoke setup_electric]
[shell electric]
  ??[info] Starting replication from postgres

# Client should be able to continue same with old offset or 0_inf offset
[shell client]
  [invoke shape_get items $handle $offset]
  ??HTTP/1.1 200 OK
  ??electric-handle: $handle
  ??[{"headers":{"control":"up-to-date","global_last_seen_lsn":

  [invoke shape_get items $handle "0_inf"]
  ??HTTP/1.1 200 OK
  ??electric-handle: $handle
  ??[{"headers":{"control":"up-to-date","global_last_seen_lsn":

# Add more data before another crash
[shell psql]
  """!
  INSERT INTO items (id, val)
  VALUES (gen_random_uuid(), '#pre-crash test val');
  """
  ??INSERT 0 1

# Should see pre crash update
[shell client]
  [invoke shape_get_live items $handle $offset]
  ??HTTP/1.1 200 OK
  ?electric-offset: ([\w\d_]+)
  [local offset=$1]
  ??"val":"#pre-crash test val"

## Crash electric again
[shell electric]
  !System.halt()
  ??$PS1

## Start the sync service again.
[invoke setup_electric]
[shell electric]
  ??[info] Starting replication from postgres
  ??[notice] Consumers ready
  # We have a small race-condition here. we rely on the un-acked transaction from pg to
  # be received by the storage in order to populate the offset information
  # (that hasn't been flushed to disk). The sqlite-based startup is slightly
  # slower than the pure-ets version (~50ms vs ~5ms) which means that this request
  # arrives before the stack has received the pending operation.
  # To mitigate this we ensure the txn has been processed.
  ??[debug] Txn received in Shapes.Consumer

# Client should be able to continue same shape
[shell client]
  [invoke shape_get items $handle $offset]
  ??HTTP/1.1 200 OK
  ??electric-handle: $handle
  ??[{"headers":{"control":"up-to-date","global_last_seen_lsn":


# Recovered shape should handle new data
[shell psql]
  """!
  INSERT INTO items (id, val)
  VALUES (gen_random_uuid(), '#post-crash test val');
  """
  ??INSERT 0 1

[shell client]
  [invoke shape_get_live items $handle $offset]
  ??HTTP/1.1 200 OK
  ??electric-handle: $handle
  ??"val":"#post-crash test val"

[cleanup]
  [invoke teardown]
