[doc Test one direction replication from pg_1 to pg_2]

[global fail_pattern=[Ee][Rr][Rr][Oo][Rr]]
[global psql=electric]

[include shared.luxinc]
[invoke setup]

[shell pg_3]
    [invoke start_psql pg_3]

[shell electric_b]
    [timeout 10]
    !make start_electric_2
    ?Starting replication to postgres_3

[shell pg_1]
    !INSERT INTO entries (content) VALUES ('Hello from a');
    ?INSERT 0 1

[shell electric_b]
    ?pg_slot=postgres_3 origin=postgres_3 \[debug\] Sending \d messages to the subscriber

[shell pg_3]
    !SELECT * FROM entries;
    ?Hello from a
    ?(1 row)

[shell pg_3]
    !INSERT INTO entries (content) VALUES ('Hello from c');
    ?INSERT 0 1

[shell electric]
    ?+pg_slot=postgres_2 origin=postgres_2 \[debug\] Sending \d messages to the subscriber
    ?pg_slot=postgres_1 origin=postgres_1 \[debug\] Sending \d messages to the subscriber
    [timeout 10]

[shell pg_1]
    [loop iter 1..10]
        @Hello from c
        !select * from entries;
        ?electric=
        [sleep 1]
    [endloop]

[shell pg_2]
    [loop iter 1..10]
        @Hello from c
        !select * from entries;
        ?electric=
        [sleep 1]
    [endloop]

[cleanup]
    [invoke teardown]

