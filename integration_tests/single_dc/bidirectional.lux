[doc Bidirectional replication test pg_1 <-> pg_2]

[global fail_pattern=[Ee][Rr][Rr][Oo][Rr]]
[global psql=electric]

[include shared.luxinc]

[invoke setup]

[shell pg_1]
    !INSERT INTO entries (content) VALUES ('hello from pg_1');
    ?$psql

[shell pg_2]
    [loop iter 1..10]
        @hello from pg_1
        !select * from entries;
        ?electric=
        [sleep 1]
    [endloop]

[shell electric]
    ?"content" => "hello from pg_1"

[shell pg_2]
    !INSERT INTO entries (content) VALUES ('hello from pg_2');
    ?$psql

[shell electric]
    ?NewRecord
    ?"content" => "hello from pg_2"

[shell pg_1]
    [loop iter 1..10]
        @hello from pg_2
        !select * from entries;
        ?electric=
        [sleep 1]
    [endloop]
    !UPDATE entries SET content_b = 'bye from pg_1';
    ?$psql

[shell electric]
    ?UpdatedRecord
    ?"content_b" => "bye from pg_1"

[shell pg_2]
    [loop iter 1..10]
        @bye from pg_1
        !select * from entries;
        ?electric=
        [sleep 1]
    [endloop]
    !DELETE FROM entries WHERE content_b = 'bye from pg_1';
    ?$psql

[shell electric]
    ?DeletedRecord
    ?"content_b" => "bye from pg_1"

[shell pg_2]
  [loop iter 1..10]
      @0 rows
      !select * from entries;
      ?electric=
      [sleep 1]
  [endloop]

[cleanup]
    [invoke teardown]
